plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modImplementation "com.teamresourceful.resourcefullib:resourcefullib-neoforge-1.21:$rootProject.resourcefullib_version"
    modImplementation "com.teamresourceful.resourcefulconfig:resourcefulconfig-neoforge-1.21:$rootProject.resourcefulconfig_version"
    forgeRuntimeLibrary "com.teamresourceful:bytecodecs:1.1.2"

    implementation "software.bernie.geckolib:geckolib-neoforge-$rootProject.minecraft_version:$rootProject.geckolib_version"

    modCompileOnly "mezz.jei:jei-$rootProject.minecraft_version-neoforge-api:$rootProject.jei_version"
    //modRuntimeOnly "mezz.jei:jei-$rootProject.minecraft_version-neoforge:$rootProject.jei_version"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-neoforge:$rootProject.rei_version"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-neoforge:$rootProject.rei_version"
}

loom {
    runs {
        data {
            data()
            programArgs "--all", "--mod", "$mod_id"
            programArgs "--output", file("../common/src/generated/resources").absolutePath
            programArgs "--existing", file("../common/src/main/resources").absolutePath
        }
    }
}

processResources {
    def props = [
            'mod_id'                   : rootProject.mod_id,
            'version'                  : rootProject.version,
            'minecraft_version'        : rootProject.minecraft_version,
            'architectury_api_version' : rootProject.architectury_api_version,
            'resourcefullib_version'   : rootProject.resourcefullib_version,
            'resourcefulconfig_version': rootProject.resourcefulconfig_version,
            'geckolib_version'         : rootProject.geckolib_version,
            'jei_version'              : rootProject.jei_version,
            'rei_version'              : rootProject.rei_version
    ]

    inputs.properties props

    filesMatching('META-INF/neoforge.mods.toml') {
        expand props
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
